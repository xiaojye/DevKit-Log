apply plugin: 'maven-publish'
apply plugin: 'signing'

def GROUP_ID = 'io.github.xiaojye'
def ARTIFACT_ID = 'DevKit-Log'
def VERSION_NAME = '1.0.0'
def DESCRIPTION = 'Android开发套件 - 日志组件'
def GIT_LOCATION = 'github.com/xiaojye/DevKit-Log'
def GIT_LOCATION_URL = 'https://' + GIT_LOCATION
def DEVELOPER_ID = 'jye'
def DEVELOPER_NAME = 'jye'
def DEVELOPER_EMAIL = 'ixiaojye@163.com'

//仓库地址
def MAVEN_URL = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def nexusUsername = properties.getProperty("nexusUsername")
def nexusPassword = properties.getProperty("nexusPassword")

task sourceJar(type: Jar) {
    if (hasAndroidPlugin()) {
        println "======> Android"
        from android.sourceSets.main.java.srcDirs
        //noinspection GroovyAccessibility
        archiveClassifier = 'sources'
    } else if (hasJavaPlugin()) {
        println "======> Java"
        from sourceSets.main.allSource
        //noinspection GroovyAccessibility
        archiveClassifier = 'sources'
    }
}

afterEvaluate { project ->
    tasks.all { Task task ->
        if (task.name.equalsIgnoreCase('publishAarPomPublishPublicationToSnapshotsRepository')) {
            task.dependsOn tasks.getByName('assemble')
        }
    }
}

def hasJavaPlugin() {
    if (plugins.hasPlugin("java-library")) {
        return true
    }
    return false
}

def hasAndroidPlugin() {
    if (plugins.hasPlugin("com.android.library")) {
        return true
    }
    return false
}

publishing {
    println "======> ${project.name}"
    println "====> the aar path is " + "$buildDir/outputs/aar/${project.name}-release.aar"
    println "====>" + MAVEN_URL
    println "====>" + VERSION_NAME
    publications {
        aarPomPublish(MavenPublication) {
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION_NAME
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            pom {
                packaging 'aar'
                name = ARTIFACT_ID
                description = DESCRIPTION
                url = GIT_LOCATION_URL
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = DEVELOPER_ID
                        name = DEVELOPER_NAME
                        email = DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection = 'scm:git:' + GIT_LOCATION + '.git'
                    developerConnection = 'scm:git:ssh://' + GIT_LOCATION + '.git'
                    url = GIT_LOCATION_URL
                }
            }
            pom.withXml {
                //asNode().appendNode('测试').appendNode('自定义key', '自定义value')
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.version != "unspecified") { // 过滤项目内library引用
                        println "=====>" + it.properties
                        println "=====>" + it.group + "  " + it.name + it.version
                        if (it.group == null || it.name == null || it.version == null) return
                        if (it.group == group) return
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)

                        if (it.excludeRules.size() > 0) {
                            def exclusionsNode = dependencyNode.appendNode('exclusions')
                            it.excludeRules.each { rule ->
                                def exclusionNode = exclusionsNode.appendNode('exclusion')
                                exclusionNode.appendNode('groupId', rule.group)
                                exclusionNode.appendNode('artifactId', rule.module)
                            }
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name "snapshots"
            url MAVEN_URL
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

// signing 必须在 publishing 配置之后
signing {
    sign publishing.publications.aarPomPublish
}

